Use cWebView.pkg
Use cWebPanel.pkg
Use cWebButton.pkg
Use cWebForm.pkg
Use cWebLabel.pkg
Use cWebHtmlBox.pkg
Use cTruckDataDictionary.dd
Use cli.pkg
Use cRFWebForm.pkg

Open Truck


Object oSettingsTrucks is a cWebView    
    // Your DDO structure will go here
    Set pbServerOnShow to True
    
    Object oTruckDD is a cTruckDataDictionary        
        Procedure OnConstrain
            String sCompGUID sBUGUID sConstraint
            Get psCompanyGUID of ghoWebSessionManager to sCompGUID
            Get psBusinessUnitGUID of ghoWebSessionManager to sBUGUID
            Constrain Truck.CompanyID eq  sCompGUID
            Constrain Truck.IsDeleted eq False
            //Constrain Truck.TruckCode ne ""
        End_Procedure                          
    End_Object          
    
    Set Main_DD to oTruckDD
    Set Server  to oTruckDD
    
    Object oWebMainPanel is a cWebPanel
        Set piColumnCount to 4        
        
        Object oWebLeftPanel is a cWebPanel
            Set peRegion to prLeft
            Set pbResizable to True
            Set piWidth to 200
            Set piMinWidth to 200
            Set psCSSClass to "viewLeft"
            
            Object oList is a cWebList
                Set piColumnSpan to 4
                Set pbFillHeight to True
                Set Ordering to 2
                Object oColumn is a cWebColumn
                    Set psCaption to "Trucks"
                    Entry_Item Truck.TruckCode
                End_Object

                Procedure OnChangeCurrentRow String sFromRowID String sToRowID                
                    Boolean bEnabled
                    Forward Send OnChangeCurrentRow sFromRowID sToRowID
                    WebGet pbEnabled of oCode to bEnabled
                    If (bEnabled) Begin
                        Send enableEdit False
                    End
                End_Procedure

            End_Object            
        End_Object

        Object oWebMainPanel is a cWebPanel
            Set piColumnCount to 20
            Set psCSSClass to "viewCenter"
            
            Object oHdrLine is a cWebLabel
                Set psCaption to "Setup Trucks"
                Set psCSSClass to "hdrLabel"
                Set piColumnSpan to 20
            End_Object

            Object oCode is a cRFWebForm
                Set psLabel to "Truck Code:"
                Set piColumnSpan to 10
                Set peLabelAlign to alignLeft
                Set peLabelPosition to lpLeft
                Entry_Item Truck.TruckCode
            End_Object
            
            Object oDesc is a cRFWebForm
                Set psLabel to "Truck Desc:"
                Set piColumnSpan to 10
                Set peLabelAlign to alignLeft
                Set peLabelPosition to lpLeft
                Entry_Item Truck.TruckDesc
            End_Object

            Object oCapacity is a cRFWebForm
                Set psLabel to "Capacity (tons):"
                Set piColumnSpan to 5
                Set peLabelAlign to alignLeft
                Set peLabelPosition to lpLeft
                Entry_Item Truck.Capacity
            End_Object

            Object oRFID is a cRFWebForm
                Set psLabel to "Tag ID:"
                Set piColumnSpan to 10
                Set peLabelAlign to alignLeft
                Set peLabelPosition to lpLeft
                Entry_Item Truck.RFID
            End_Object

//            Object oComID is a cWebForm
//                Set psLabel to "Company ID:"
//                Set piColumnSpan to 10
//                Set peLabelAlign to alignLeft
//                Set peLabelPosition to lpLeft
//                Entry_Item Plant.CompanyID
//            End_Object
                        
            Object oSpacer is a cWebSpacer
                Set pbFillHeight to True
                Set psCSSClass to "setupSpacer"
            End_Object

            Object oAddSaveBtn is a cWebButton
                Set psCaption to "Add Truck"
                Set piColumnSpan to 4
                    
                Procedure OnClick
                    String sLabel
                    Boolean bHasRecord bErr
                    WebGet psCaption to sLabel
                    If (sLabel = "Add Truck") Begin
                        Send Request_Clear to (Server(Self))
                        Send EnableEdit True
                        Send Focus to oCode
                    End    
                    Else Begin
                        Get Request_Validate_And_Save of (Server(Self)) to bErr
                        If (not(bErr)) Begin
                            Send EnableEdit False
                            Send Focus of oList                         
                        End
                    End
                    
                End_Procedure
            End_Object
            
            Object oEditCancelButton is a cWebButton
                Set piColumnSpan to 4
                Set piColumnIndex to 4
                Set psCaption to "Edit"
                Set psCSSClass to "editButton"
      
                Procedure OnClick
                    String sLabel
                    Boolean bHasRecord
                    WebGet psCaption to sLabel
                    If (sLabel = "Edit") Begin
                        Send enableEdit True                        
                        Send Focus to oCode
                    End
                    Else Begin                        
                        Get HasRecord of (Server(Self)) to bHasRecord
                        If (bHasRecord) Begin
                             Send Find of (Server(Self)) eq 1
                        End
                        Else Begin
                            Send Find of (Server(Self)) First_Record 5
                        End                       
                        Send enableEdit False                       
                        Send Focus of oList
                    End                    
                End_Procedure                
            End_Object
            
            Object oDeleteButton is a cWebButton
                Set piColumnSpan to 4
                Set piColumnIndex to 16
                Set psCaption to "Delete"
                Set psCSSClass to "deleteButton"

                Procedure OnClick
                    Boolean bEnabled
                    String sCode sDesc
                    Integer iResponse iErr
                    WebGet pbEnabled of oCode to bEnabled
                    If (not(bEnabled)) Begin
                        If (HasRecord(Main_DD(Self))) Begin
                            Get Field_Current_Value of (Main_DD(Self)) Field Truck.TruckDesc to sDesc
                            Send MessageDialog of oMessageDialog Self ("Delete Truck") ("Delete" * sDesc - "?") MD_YES_NO
                            Procedure_Return
                        End
                    End
                    Send Refind_Records of (Main_DD(Self))
                    Send Request_Assign of (Main_DD(Self))
                    Send EnableEdit (False)
                    
                    If (not(HasRecord(Main_DD(Self)))) Begin
                        Send MoveToFirstRow to oList    
                    End
                    Send Focus to oList
                End_Procedure
                
                Procedure OnCloseModalDialog Handle hoModalDialog
                    Boolean bEnabled
                    String sLoginName
                    Integer iResponse iErr
                    WebGet MD_RESULT of oMessageDialog to iResponse
                    If (iResponse ne MDR_YES) Begin
                        Procedure_Return
                    End
                    WebGet pbEnabled of oCode to bEnabled
                    If ( (not(bEnabled)) and (HasRecord(Main_DD(Self)))) Begin
                        Get Validate_Delete of (Main_DD(Self)) to iErr
                        If (not(iErr)) Begin
                            Send Request_Delete of (Main_DD(Self))
                            Send Request_Clear of (Main_DD(Self))
                            Send MoveToFirstRow to oList    
                        End
                            Else Procedure_Return
                    End
                    Send EnableEdit False
                End_Procedure
            End_Object
            
        End_Object
    End_Object 
    
    Procedure EnableEdit Boolean bEnabled
        Boolean bAllowed
        Get piIsAdmin of ghoWebSessionManager to bAllowed
        If (not(bAllowed)) Begin
            WebSet pbEnabled of oAddSaveBtn to False
            WebSet pbEnabled of oEditCancelButton to False
            WebSet pbEnabled of oDeleteButton to False
            Procedure_Return
        End        
        String sVal
        WebSet pbEnabled of oCode to bEnabled
        WebSet pbEnabled of oDesc to bEnabled
        WebSet pbEnabled of oCapacity to bEnabled
        WebSet pbEnabled of oRFID to bEnabled
        If (bEnabled) Begin
            WebSet psCaption of oAddSaveBtn     to "Save"
            WebSet psCaption of oEditCancelButton   to "Cancel"
        End
        Else Begin
            WebSet psCaption of oAddSaveBtn     to "Add Truck"
            WebSet psCaption of oEditCancelButton   to "Edit"
        End
    End_Procedure
    
    Procedure OnShow
        Send EnableEdit False
        Send Rebuild_Constraints to  oTruckDD //(Main_DD(Self))
        Send MoveToFirstRow to oList        
    End_Procedure
End_Object
